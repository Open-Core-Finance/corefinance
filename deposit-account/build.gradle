buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        dependencies {
            classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootPluginVersion")
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootPluginVersion")
        classpath "io.spring.gradle:dependency-management-plugin:$springDependenciesManagementVersion"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:latest.release"
        classpath "io.freefair.gradle:lombok-plugin:latest.release"
    }
}

plugins {
	id 'java'
    id 'java-library'
	id 'org.springframework.boot' version "$springBootPluginVersion"
	id 'io.spring.dependency-management' version "$springDependenciesManagementVersion"
    id 'jacoco'
    id "io.freefair.lombok" version "${lombokPluginVersion}"
    id "org.sonarqube" version "${sonarqubePluginVersion}"
    id 'maven-publish'
    id 'signing'
    id("org.hibernate.orm") version "${hibernatePluginVersion}"
}

group = 'tech.corefinance'
version = "${internalProductVersion}"

sourceCompatibility = javaSourceCompatibility
targetCompatibility = javaSourceCompatibility
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(javaSourceCompatibility))
    }
}

repositories {
	mavenLocal()
	mavenCentral()
    maven {
        url = "https://s01.oss.sonatype.org/content/groups/public/"
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootPluginVersion"
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

jar {
    archiveClassifier = ""
    exclude("*.der")
    exclude("*.xml")
    exclude("*.yaml")
    exclude("db")
    exclude("resources")
    exclude("tech/corefinance/account/gl/GlAccountApplication.class")
}

bootJar {
    archiveClassifier = "boot"
    enabled = true
}

dependencies {
    // Spring boot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'

    // Internal
    implementation project(":common-account")
//    implementation project(":microservice-common-jpa")
     implementation group: 'tech.corefinance', name: 'microservice-common', version: "${internalCommonVersion}"
     implementation group: 'tech.corefinance', name: 'microservice-common-jpa', version: "${internalCommonJpaVersion}"

    // Database
    implementation group: 'org.postgresql', name: 'postgresql', version: "${postgresqlVersion}"

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: "${powerMockitoVersion}"
    testImplementation group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo', version: "${embededMongoVersion}"
}

jacoco {
    toolVersion = "${jacocoToolVersion}"
    reportsDirectory = layout.buildDirectory.dir("jacoco").get()
}

test {

    useJUnitPlatform()

    // report is always generated after tests run
    finalizedBy jacocoTestReport

    jacoco {
        classDumpDir = layout.buildDirectory.file("jacoco/classpathdumps").get().asFile
        enabled = true
        destinationFile = layout.buildDirectory.file("jacoco/${name}.exec").get().asFile
        includes = []
        excludes = []
        excludeClassLoaders = []
        includeNoLocationClasses = false
        sessionId = UUID.randomUUID().toString()
        dumpOnExit = true
        classDumpDir = null
        output = JacocoTaskExtension.Output.FILE
        address = "localhost"
        port = 6300
        jmx = false
    }
}

jacocoTestReport {
    // tests are required to run before generating the report
    dependsOn test
    mustRunAfter test

    reports {
        html.required = true
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir("jacoco/html").get().asFile
        xml.outputLocation = layout.buildDirectory.file("jacoco/jacoco-report.xml").get().asFile
        csv.outputLocation = layout.buildDirectory.file("jacoco/jacoco-report.csv").get().asFile
    }

    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/ex/*', '**/pojo/*', '**/model/*', '**/dto/*', '**/config/*'])
        })
    }
}

task javadocJar (type :Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier='javadoc'
    description='Creates a Javadoc Jar'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? "${artifactSnapshotUrl}" : "${artifactReleaseUrl}"
            credentials {
                username = "${artifactUsername}"
                password = "${artifactPassword}"
            }
            allowInsecureProtocol = true
        }
    }
    publications {
        group = group
        version = version

        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Core finance Deposit Account'
                description = 'Deposit account manager.'
                url = 'https://corefinance.tech/'
                properties = [ "lib.name": "deposit-account" ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'dbaotrung'
                        name = 'Trung Doan'
                        email = 'doanbaotrung@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:ssh://git@github.com:Open-Core-Finance/corefinance.git'
                    developerConnection = 'scm:git:ssh://git@github.com:Open-Core-Finance/corefinance.git'
                    url = 'https://github.com/Open-Core-Finance/corefinance'
                }
            }
        }
    }
}

publish.dependsOn(':deposit-account:build')
publish.mustRunAfter(':deposit-account:build')

signing {
    sign publishing.publications
}